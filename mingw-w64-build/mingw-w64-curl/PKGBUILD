# Changes for saldl:
# * Remove a part of a patch that actually breaks static builds
# * Skip building shared libs
# * Use a workaround to stop wine from interfering with builds
# * Keep this independently updated

# Maintainer: drakkan <nicola.murino at gmail dot com>
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: ant32 <antreimer at gmail dot com>

pkgname=mingw-w64-curl
pkgver=8.8.0
pkgrel=1
pkgdesc="An URL retrival utility and library (mingw-w64)"
arch=('any')
url="https://curl.haxx.se"
license=("MIT")
depends=('mingw-w64-crt'
         'mingw-w64-libidn2'
         'mingw-w64-libpsl'
         'mingw-w64-openssl'
         'mingw-w64-libnghttp2'
         'mingw-w64-zstd'
         'mingw-w64-brotli'
         'mingw-w64-zlib')
makedepends=('mingw-w64-configure')
options=('staticlibs' '!strip' '!buildflags')
source=("${url}/download/curl-${pkgver}.tar.xz"
        "0002-nghttp2-static.patch"
        "0004-more-static-fixes.patch")
sha256sums=('0f58bb95fc330c8a46eeb3df5701b0d90c9d9bfcc42bd1cd08791d12551d4400'
            '3ee9c75a3046f86f91290c143170179230c9adc6eabfbb79eb26f708a165b719'
            '74bef2c9995765e2864d257deca5769aed15b697eb46a6508cdece15f26a7b29')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}
  patch -Np1 -i "${srcdir}/0002-nghttp2-static.patch"
  patch -Np1 -i "${srcdir}/0004-more-static-fixes.patch"
  autoreconf -vfi
}

build() {
  cd "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}

  # Avoid wine interfering if installed (suggested by AUR user maksverver <maks at verver.ch>)
  export CHOST=$(./config.guess)

  for _arch in ${_architectures}; do
    configure_args="--with-openssl --enable-ipv6 --with-libidn2  --without-ca-bundle --without-random --with-libpsl --with-brotli --with-zstd"
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-configure $configure_args \
      --enable-static --disable-shared ..
    make
    popd
    #mkdir -p build-${_arch} && pushd build-${_arch}
    #${_arch}-configure $configure_args \
    #  --disable-static --enable-shared ..
    #make
    #popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname#mingw-w64-}-${pkgver}/build-${_arch}-static"
    make DESTDIR="${pkgdir}" install
    #cd "${srcdir}/${pkgname#mingw-w64-}-${pkgver}/build-${_arch}"
    #make DESTDIR="${pkgdir}" install
    #rm "$pkgdir"/usr/${_arch}/bin/*.exe
    rm -r "${pkgdir}/usr/${_arch}/share"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.exe
    #${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
